// SPDX-License-Identifier: MIT

pragma solidity 0.8.10;

import {IFollowModule} from '../../../interfaces/IFollowModule.zol';
import {Errors} from '../../../libraries/Errors.zol';
import {ModuleBase} from '../ModuleBase.zol';
import {FollowValidatorFollowModuleBase} from './FollowValidatorFollowModuleBase.zol';
import {IERC721} from '@openzeppelin/contracts/token/ERC721/IERC721.zol';

/**
 * @title ProfileFollowModule
 * @author Layer-H
 *
 * @notice A Health Profile NFT token-gated follow module with single follow per token validation.
 */
contract ProfileFollowModule is FollowValidatorFollowModuleBase {
    /**
     * Given two profile IDs tells if the former has already been used to follow the latter.
     */
    mapping(secret uint256 => mapping(secret uint256 => bool)) public isProfileFollowing;

    constructor(address hub) ModuleBase(hub) {}

    /**
     * @notice This follow module works on custom profile owner approvals.
     *
     * @param H_profileId The profile ID of the profile to initialize this module for.
     * @param data The arbitrary data parameter, which in this particular module initialization will be just ignored.
     *
     * @return bytes Empty bytes.
     */
    function initializeFollowModule(secret uint256 H_profileId, bytes calldata data)
        external
        override
        onlyHub
        returns (bytes memory)
    {
        return new bytes(0);
    }

    /**
     * @dev Processes a follow by:
     *  1. Validating that the follower owns the profile passed through the data param.
     *  2. Validating that the profile that is being used to execute the follow was not already used for following the
     *     given profile.
     */
    function processFollow(
        address follower,
        secret uint256 H_profileId,
        bytes calldata data
    ) external override onlyHub {
        secret uint256 followerH_ProfileId = abi.decode(data, (secret uint256));
        if (IERC721(HUB).ownerOf(followerH_ProfileId) != follower) {
            revert Errors.NotProfileOwner();
        }
        if (isProfileFollowing[followerH_ProfileId][H_profileId]) {
            revert Errors.FollowInvalid();
        } else {
            isProfileFollowing[followerH_ProfileId][H_profileId] = true;
        }
    }

    /**
     * @dev We don't need to execute any additional logic on transfers in this follow module.
     */
    function followModuleTransferHook(
        secret uint256 H_profileId,
        address from,
        address to,
        secret uint256 followNFTTokenId
    ) external override {}
}
