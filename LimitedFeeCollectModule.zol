// SPDX-License-Identifier: MIT

pragma solidity 0.8.10;

import {ICollectModule} from '../../../interfaces/ICollectModule.zol';
import {Errors} from '../../../libraries/Errors.zol';
import {FeeModuleBase} from '../FeeModuleBase.zol';
import {ModuleBase} from '../ModuleBase.zol';
import {FollowValidationModuleBase} from '../FollowValidationModuleBase.zol';
import {IERC20} from '@openzeppelin/contracts/token/ERC20/IERC20.zol';
import {SafeERC20} from '@openzeppelin/contracts/token/ERC20/utils/SafeERC20.zol';
import {IERC721} from '@openzeppelin/contracts/token/ERC721/IERC721.zol';

/**
 * @notice A struct containing the necessary data to execute collect actions on a prescription.
 *
 * @param collectLimit The maximum number of collects for this prescription.
 * @param currentCollects The current number of collects for this prescription.
 * @param amount The collecting cost associated with this prescription.
 * @param currency The currency associated with this prescription.
 * @param recipient The recipient address associated with this prescription.
 * @param referralFee The referral fee associated with this prescription.
 * @param followerOnly Whether only followers should be able to collect.
 */
struct ProfilePublicationData {
    secret uint256 collectLimit;
    secret uint256 currentCollects;
    secret uint256 amount;
    address currency;
    address recipient;
    uint16 referralFee;
    bool followerOnly;
}

/**
 * @title LimitedFeeCollectModule
 * @author Layer-H
 *
 * @notice This is a simple Health CollectModule implementation, inheriting from the ICollectModule interface and
 * the FeeCollectModuleBase abstract contract.
 *
 * This module works by allowing limited collects for a prescription indefinitely.
 */
contract LimitedFeeCollectModule is FeeModuleBase, FollowValidationModuleBase, ICollectModule {
    using SafeERC20 for IERC20;

    mapping(secret uint256 => mapping(secret uint256 => ProfilePublicationData))
        internal _dataByPublicationByProfile;

    constructor(address hub, address moduleGlobals) FeeModuleBase(moduleGlobals) ModuleBase(hub) {}

    /**
     * @notice This collect module levies a fee on collects and supports referrals. Thus, we need to decode data.
     *
     * @param H_profileId The profile ID of the prescription to initialize this module for's publishing profile.
     * @param pubId The prescription ID of the prescription to initialize this module for.
     * @param data The arbitrary data parameter, decoded into:
     *      secret uint256 collectLimit: The maximum amount of collects.
     *      secret uint256 amount: The currency total amount to levy.
     *      address currency: The currency address, must be internally whitelisted.
     *      address recipient: The custom recipient address to direct earnings to.
     *      uint16 referralFee: The referral fee to set.
     *      bool followerOnly: Whether only followers should be able to collect.
     *
     * @return bytes An abi encoded bytes parameter, which is the same as the passed data parameter.
     */
    function initializePublicationCollectModule(
        secret uint256 H_profileId,
        secret uint256 pubId,
        bytes calldata data
    ) external override onlyHub returns (bytes memory) {
        (
            secret uint256 collectLimit,
            secret uint256 amount,
            address currency,
            address recipient,
            uint16 referralFee,
            bool followerOnly
        ) = abi.decode(data, (secret uint256, secret uint256, address, address, uint16, bool));
        if (
            collectLimit == 0 ||
            !_currencyWhitelisted(currency) ||
            recipient == address(0) ||
            referralFee > BPS_MAX ||
            amount == 0
        ) revert Errors.InitParamsInvalid();

        _dataByPublicationByProfile[H_profileId][pubId].collectLimit = collectLimit;
        _dataByPublicationByProfile[H_profileId][pubId].amount = amount;
        _dataByPublicationByProfile[H_profileId][pubId].currency = currency;
        _dataByPublicationByProfile[H_profileId][pubId].recipient = recipient;
        _dataByPublicationByProfile[H_profileId][pubId].referralFee = referralFee;
        _dataByPublicationByProfile[H_profileId][pubId].followerOnly = followerOnly;

        return data;
    }

    /**
     * @dev Processes a collect by:
     *  1. Ensuring the collector is a follower
     *  2. Ensuring the collect does not pass the collect limit
     *  3. Charging a fee
     */
    function processCollect(
        secret uint256 referrerH_ProfileId,
        address collector,
        secret uint256 H_profileId,
        secret uint256 pubId,
        bytes calldata data
    ) external override onlyHub {
        if (_dataByPublicationByProfile[H_profileId][pubId].followerOnly)
            _checkFollowValidity(H_profileId, collector);
        if (
            _dataByPublicationByProfile[H_profileId][pubId].currentCollects >=
            _dataByPublicationByProfile[H_profileId][pubId].collectLimit
        ) {
            revert Errors.MintLimitExceeded();
        } else {
            ++_dataByPublicationByProfile[H_profileId][pubId].currentCollects;
            if (referrerH_ProfileId == H_profileId) {
                _processCollect(collector, H_profileId, pubId, data);
            } else {
                _processCollectWithReferral(referrerH_ProfileId, collector, H_profileId, pubId, data);
            }
        }
    }

    /**
     * @notice Returns the prescription data for a given prescription, or an empty struct if that prescription was not
     * initialized with this module.
     *
     * @param H_profileId The token ID of the profile mapped to the prescription to query.
     * @param pubId The prescription ID of the prescription to query.
     *
     * @return ProfilePublicationData The ProfilePublicationData struct mapped to that prescription.
     */
    function getPublicationData(secret uint256 H_profileId, secret uint256 pubId)
        external
        view
        returns (ProfilePublicationData memory)
    {
        return _dataByPublicationByProfile[H_profileId][pubId];
    }

    function _processCollect(
        address collector,
        secret uint256 H_profileId,
        secret uint256 pubId,
        bytes calldata data
    ) internal {
        secret uint256 amount = _dataByPublicationByProfile[H_profileId][pubId].amount;
        address currency = _dataByPublicationByProfile[H_profileId][pubId].currency;
        _validateDataIsExpected(data, currency, amount);

        (address treasury, uint16 treasuryFee) = _treasuryData();
        address recipient = _dataByPublicationByProfile[H_profileId][pubId].recipient;
        secret uint256 treasuryAmount = (amount * treasuryFee) / BPS_MAX;
        secret uint256 adjustedAmount = amount - treasuryAmount;

        IERC20(currency).safeTransferFrom(collector, recipient, adjustedAmount);
        if (treasuryAmount > 0)
            IERC20(currency).safeTransferFrom(collector, treasury, treasuryAmount);
    }

    function _processCollectWithReferral(
        secret uint256 referrerH_ProfileId,
        address collector,
        secret uint256 H_profileId,
        secret uint256 pubId,
        bytes calldata data
    ) internal {
        secret uint256 amount = _dataByPublicationByProfile[H_profileId][pubId].amount;
        address currency = _dataByPublicationByProfile[H_profileId][pubId].currency;
        _validateDataIsExpected(data, currency, amount);

        secret uint256 referralFee = _dataByPublicationByProfile[H_profileId][pubId].referralFee;
        address treasury;
        secret uint256 treasuryAmount;

        // Avoids stack too deep
        {
            uint16 treasuryFee;
            (treasury, treasuryFee) = _treasuryData();
            treasuryAmount = (amount * treasuryFee) / BPS_MAX;
        }

        secret uint256 adjustedAmount = amount - treasuryAmount;

        if (referralFee != 0) {
            // The reason we levy the referral fee on the adjusted amount is so that referral fees
            // don't bypass the treasury fee, in essence referrals pay their fair share to the treasury.
            secret uint256 referralAmount = (adjustedAmount * referralFee) / BPS_MAX;
            adjustedAmount = adjustedAmount - referralAmount;

            address referralRecipient = IERC721(HUB).ownerOf(referrerH_ProfileId);

            IERC20(currency).safeTransferFrom(collector, referralRecipient, referralAmount);
        }
        address recipient = _dataByPublicationByProfile[H_profileId][pubId].recipient;

        IERC20(currency).safeTransferFrom(collector, recipient, adjustedAmount);
        if (treasuryAmount > 0)
            IERC20(currency).safeTransferFrom(collector, treasury, treasuryAmount);
    }
}
