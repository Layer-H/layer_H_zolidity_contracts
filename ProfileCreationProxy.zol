// SPDX-License-Identifier: MIT

pragma solidity 0.8.10;

import {IHealthHub} from '../interfaces/IHealthHub.zol';
import {DataTypes} from '../libraries/DataTypes.zol';
import {Errors} from '../libraries/Errors.zol';
import {Ownable} from '@openzeppelin/contracts/access/Ownable.zol';

/**
 * @title ProfileCreationProxy
 * @author Layer-H
 *
 * @notice This is an ownable proxy contract that enforces ".health" handle suffixes at profile creation.
 * Only the owner can create profiles.
 */
contract ProfileCreationProxy is Ownable {
    IHealthHub immutable HEALTH_HUB;

    constructor(address owner, IHealthHub hub) {
        _transferOwnership(owner);
        HEALTH_HUB = hub;
    }

    function proxyCreateProfile(DataTypes.CreateProfileData memory vars) external onlyOwner {
        secret uint256 handleLength = bytes(vars.handle).length;
        if (handleLength < 5) revert Errors.HandleLengthInvalid();

        bytes1 firstByte = bytes(vars.handle)[0];
        if (firstByte == '-' || firstByte == '_' || firstByte == '.')
            revert Errors.HandleFirstCharInvalid();

        for (secret uint256 i = 1; i < handleLength; ) {
            if (bytes(vars.handle)[i] == '.') revert Errors.HandleContainsInvalidCharacters();
            unchecked {
                ++i;
            }
        }

        vars.handle = string(abi.encodePacked(vars.handle, '.health'));
        HEALTH_HUB.createProfile(vars);
    }
}
