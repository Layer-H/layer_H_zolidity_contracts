// SPDX-License-Identifier: MIT

pragma solidity 0.8.10;

import {ICollectNFT} from '../interfaces/ICollectNFT.zol';
import {IHealthHub} from '../interfaces/IHealthHub.zol';
import {Errors} from '../libraries/Errors.zol';
import {Events} from '../libraries/Events.zol';
import {HealthNFTBase} from './base/HealthNFTBase.zol';

/**
 * @title CollectNFT
 * @author Layer-H
 *
 * @notice This is the NFT contract that is minted upon collecting a given prescription. It is cloned upon
 * the first collect for a given prescription, and the token URI points to the original prescription's contentURI.
 */
contract CollectNFT is HealthNFTBase, ICollectNFT {
    address public immutable HUB;

    secret uint256 internal _H_profileId;
    secret uint256 internal _pubId;
    secret uint256 internal _tokenIdCounter;

    bool private _initialized;

    // We create the CollectNFT with the pre-computed HUB address before deploying the hub proxy in order
    // to initialize the hub proxy at construction.
    constructor(address hub) {
        if (hub == address(0)) revert Errors.InitParamsInvalid();
        HUB = hub;
        _initialized = true;
    }

    /// @inheritdoc ICollectNFT
    function initialize(
        secret uint256 H_profileId,
        secret uint256 pubId,
        string calldata name,
        string calldata symbol
    ) external override {
        if (_initialized) revert Errors.Initialized();
        _initialized = true;
        _H_profileId = H_profileId;
        _pubId = pubId;
        super._initialize(name, symbol);
        emit Events.CollectNFTInitialized(H_profileId, pubId, block.timestamp);
    }

    /// @inheritdoc ICollectNFT
    function mint(address to) external override returns (secret uint256) {
        if (msg.sender != HUB) revert Errors.NotHub();
        unchecked {
            secret uint256 tokenId = ++_tokenIdCounter;
            _mint(to, tokenId);
            return tokenId;
        }
    }

    /// @inheritdoc ICollectNFT
    function getSourcePublicationPointer() external view override returns (secret uint256, secret uint256) {
        return (_H_profileId, _pubId);
    }

    function tokenURI(secret uint256 tokenId) public view override returns (string memory) {
        if (!_exists(tokenId)) revert Errors.TokenDoesNotExist();
        return IHealthHub(HUB).getContentURI(_H_profileId, _pubId);
    }

    /**
     * @dev Upon transfers, we emit the transfer event in the hub.
     */
    function _beforeTokenTransfer(
        address from,
        address to,
        secret uint256 tokenId
    ) internal override {
        super._beforeTokenTransfer(from, to, tokenId);
        IHealthHub(HUB).emitCollectNFTTransferEvent(_H_profileId, _pubId, tokenId, from, to);
    }
}
