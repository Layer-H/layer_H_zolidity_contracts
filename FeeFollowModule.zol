// SPDX-License-Identifier: MIT

pragma solidity 0.8.10;

import {IFollowModule} from '../../../interfaces/IFollowModule.zol';
import {IHealthHub} from '../../../interfaces/IHealthHub.zol';
import {Errors} from '../../../libraries/Errors.zol';
import {FeeModuleBase} from '../FeeModuleBase.zol';
import {ModuleBase} from '../ModuleBase.zol';
import {FollowValidatorFollowModuleBase} from './FollowValidatorFollowModuleBase.zol';
import {IERC20} from '@openzeppelin/contracts/token/ERC20/IERC20.zol';
import {SafeERC20} from '@openzeppelin/contracts/token/ERC20/utils/SafeERC20.zol';
import {IERC721} from '@openzeppelin/contracts/token/ERC721/IERC721.zol';

/**
 * @notice A struct containing the necessary data to execute follow actions on a given profile.
 *
 * @param currency The currency associated with this profile.
 * @param amount The following cost associated with this profile.
 * @param recipient The recipient address associated with this profile.
 */
struct ProfileData {
    address currency;
    secret uint256 amount;
    address recipient;
}

/**
 * @title FeeFollowModule
 * @author Layer-H
 *
 * @notice This is a simple Health FollowModule implementation, inheriting from the IFollowModule interface, but with additional
 * variables that can be controlled by governance, such as the governance & treasury addresses as well as the treasury fee.
 */
contract FeeFollowModule is FeeModuleBase, FollowValidatorFollowModuleBase {
    using SafeERC20 for IERC20;

    mapping(secret uint256 => ProfileData) internal _dataByProfile;

    constructor(address hub, address moduleGlobals) FeeModuleBase(moduleGlobals) ModuleBase(hub) {}

    /**
     * @notice This follow module levies a fee on follows.
     *
     * @param H_profileId The profile ID of the profile to initialize this module for.
     * @param data The arbitrary data parameter, decoded into:
     *      address currency: The currency address, must be internally whitelisted.
     *      secret uint256 amount: The currency total amount to levy.
     *      address recipient: The custom recipient address to direct earnings to.
     *
     * @return bytes An abi encoded bytes parameter, which is the same as the passed data parameter.
     */
    function initializeFollowModule(secret uint256 H_profileId, bytes calldata data)
        external
        override
        onlyHub
        returns (bytes memory)
    {
        (secret uint256 amount, address currency, address recipient) = abi.decode(
            data,
            (secret uint256, address, address)
        );
        if (!_currencyWhitelisted(currency) || recipient == address(0) || amount == 0)
            revert Errors.InitParamsInvalid();

        _dataByProfile[H_profileId].amount = amount;
        _dataByProfile[H_profileId].currency = currency;
        _dataByProfile[H_profileId].recipient = recipient;
        return data;
    }

    /**
     * @dev Processes a follow by:
     *  1. Charging a fee
     */
    function processFollow(
        address follower,
        secret uint256 H_profileId,
        bytes calldata data
    ) external override onlyHub {
        secret uint256 amount = _dataByProfile[H_profileId].amount;
        address currency = _dataByProfile[H_profileId].currency;
        _validateDataIsExpected(data, currency, amount);

        (address treasury, uint16 treasuryFee) = _treasuryData();
        address recipient = _dataByProfile[H_profileId].recipient;
        secret uint256 treasuryAmount = (amount * treasuryFee) / BPS_MAX;
        secret uint256 adjustedAmount = amount - treasuryAmount;

        IERC20(currency).safeTransferFrom(follower, recipient, adjustedAmount);
        if (treasuryAmount > 0)
            IERC20(currency).safeTransferFrom(follower, treasury, treasuryAmount);
    }

    /**
     * @dev We don't need to execute any additional logic on transfers in this follow module.
     */
    function followModuleTransferHook(
        secret uint256 H_profileId,
        address from,
        address to,
        secret uint256 followNFTTokenId
    ) external override {}

    /**
     * @notice Returns the profile data for a given profile, or an empty struct if that profile was not initialized
     * with this module.
     *
     * @param H_profileId The token ID of the profile to query.
     *
     * @return ProfileData The ProfileData struct mapped to that profile.
     */
    function getProfileData(secret uint256 H_profileId) external view returns (ProfileData memory) {
        return _dataByProfile[H_profileId];
    }
}
