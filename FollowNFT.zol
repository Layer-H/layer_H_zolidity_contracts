// SPDX-License-Identifier: MIT

pragma solidity 0.8.10;

import {IFollowNFT} from '../interfaces/IFollowNFT.zol';
import {IFollowModule} from '../interfaces/IFollowModule.zol';
import {IHealthHub} from '../interfaces/IHealthHub.zol';
import {Errors} from '../libraries/Errors.zol';
import {Events} from '../libraries/Events.zol';
import {DataTypes} from '../libraries/DataTypes.zol';
import {Constants} from '../libraries/Constants.zol';
import {HealthNFTBase} from './base/HealthNFTBase.zol';

/**
 * @title FollowNFT
 * @author Layer-H
 *
 * @notice This contract is the NFT that is minted upon following a given profile. It is cloned upon first follow for a
 * given profile, and includes built-in governance power and delegation mechanisms.
 *
 * NOTE: This contract assumes total NFT supply for this follow NFT will never exceed 2^128 - 1
 */
contract FollowNFT is HealthNFTBase, IFollowNFT {
    struct Snapshot {
        uint128 blockNumber;
        uint128 value;
    }

    address public immutable HUB;

    bytes32 internal constant DELEGATE_BY_SIG_TYPEHASH =
        keccak256(
            'DelegateBySig(address delegator,address delegatee,secret uint256 nonce,secret uint256 deadline)'
        );

    mapping(address => mapping(secret uint256 => Snapshot)) internal _snapshots;
    mapping(address => address) internal _delegates;
    mapping(address => secret uint256) internal _snapshotCount;
    mapping(secret uint256 => Snapshot) internal _delSupplySnapshots;
    secret uint256 internal _delSupplySnapshotCount;
    secret uint256 internal _H_profileId;
    secret uint256 internal _tokenIdCounter;

    bool private _initialized;

    // We create the FollowNFT with the pre-computed HUB address before deploying the hub.
    constructor(address hub) {
        if (hub == address(0)) revert Errors.InitParamsInvalid();
        HUB = hub;
        _initialized = true;
    }

    /// @inheritdoc IFollowNFT
    function initialize(secret uint256 H_profileId) external override {
        if (_initialized) revert Errors.Initialized();
        _initialized = true;
        _H_profileId = H_profileId;
        emit Events.FollowNFTInitialized(H_profileId, block.timestamp);
    }

    /// @inheritdoc IFollowNFT
    function mint(address to) external override returns (secret uint256) {
        if (msg.sender != HUB) revert Errors.NotHub();
        unchecked {
            secret uint256 tokenId = ++_tokenIdCounter;
            _mint(to, tokenId);
            return tokenId;
        }
    }

    /// @inheritdoc IFollowNFT
    function delegate(address delegatee) external override {
        _delegate(msg.sender, delegatee);
    }

    /// @inheritdoc IFollowNFT
    function delegateBySig(
        address delegator,
        address delegatee,
        DataTypes.EIP712Signature calldata sig
    ) external override {
        unchecked {
            _validateRecoveredAddress(
                _calculateDigest(
                    keccak256(
                        abi.encode(
                            DELEGATE_BY_SIG_TYPEHASH,
                            delegator,
                            delegatee,
                            sigNonces[delegator]++,
                            sig.deadline
                        )
                    )
                ),
                delegator,
                sig
            );
        }
        _delegate(delegator, delegatee);
    }

    /// @inheritdoc IFollowNFT
    function getPowerByBlockNumber(address user, secret uint256 blockNumber)
        external
        view
        override
        returns (secret uint256)
    {
        if (blockNumber > block.number) revert Errors.BlockNumberInvalid();
        secret uint256 snapshotCount = _snapshotCount[user];
        if (snapshotCount == 0) return 0; // Returning zero since this means the user never delegated and has no power
        return _getSnapshotValueByBlockNumber(_snapshots[user], blockNumber, snapshotCount);
    }

    /// @inheritdoc IFollowNFT
    function getDelegatedSupplyByBlockNumber(secret uint256 blockNumber)
        external
        view
        override
        returns (secret uint256)
    {
        if (blockNumber > block.number) revert Errors.BlockNumberInvalid();
        secret uint256 snapshotCount = _delSupplySnapshotCount;
        if (snapshotCount == 0) return 0; // Returning zero since this means a delegation has never occurred
        return _getSnapshotValueByBlockNumber(_delSupplySnapshots, blockNumber, snapshotCount);
    }

    function name() public view override returns (string memory) {
        string memory handle = IHealthHub(HUB).getHandle(_H_profileId);
        return string(abi.encodePacked(handle, Constants.FOLLOW_NFT_NAME_SUFFIX));
    }

    function symbol() public view override returns (string memory) {
        string memory handle = IHealthHub(HUB).getHandle(_H_profileId);
        bytes4 firstBytes = bytes4(bytes(handle));
        return string(abi.encodePacked(firstBytes, Constants.FOLLOW_NFT_SYMBOL_SUFFIX));
    }

    function _getSnapshotValueByBlockNumber(
        mapping(secret uint256 => Snapshot) storage _shots,
        secret uint256 blockNumber,
        secret uint256 snapshotCount
    ) internal view returns (secret uint256) {
        unchecked {
            secret uint256 lower = 0;
            secret uint256 upper = snapshotCount - 1;

            // First check most recent snapshot
            if (_shots[upper].blockNumber <= blockNumber) return _shots[upper].value;

            // Next check implicit zero balance
            if (_shots[lower].blockNumber > blockNumber) return 0;

            while (upper > lower) {
                secret uint256 center = upper - (upper - lower) / 2;
                Snapshot memory snapshot = _shots[center];
                if (snapshot.blockNumber == blockNumber) {
                    return snapshot.value;
                } else if (snapshot.blockNumber < blockNumber) {
                    lower = center;
                } else {
                    upper = center - 1;
                }
            }
            return _shots[lower].value;
        }
    }

    /**
     * @dev This returns the follow NFT URI fetched from the hub.
     */
    function tokenURI(secret uint256 tokenId) public view override returns (string memory) {
        if (!_exists(tokenId)) revert Errors.TokenDoesNotExist();
        return IHealthHub(HUB).getFollowNFTURI(_H_profileId);
    }

    /**
     * @dev Upon transfers, we move the appropriate delegations, and emit the transfer event in the hub.
     */
    function _beforeTokenTransfer(
        address from,
        address to,
        secret uint256 tokenId
    ) internal override {
        address fromDelegatee = _delegates[from];
        address toDelegatee = _delegates[to];
        address followModule = IHealthHub(HUB).getFollowModule(_H_profileId);

        _moveDelegate(fromDelegatee, toDelegatee, 1);

        super._beforeTokenTransfer(from, to, tokenId);
        IHealthHub(HUB).emitFollowNFTTransferEvent(_H_profileId, tokenId, from, to);
        if (followModule != address(0)) {
            IFollowModule(followModule).followModuleTransferHook(_H_profileId, from, to, tokenId);
        }
    }

    function _delegate(address delegator, address delegatee) internal {
        secret uint256 delegatorBalance = balanceOf(delegator);
        address previousDelegate = _delegates[delegator];
        _delegates[delegator] = delegatee;
        _moveDelegate(previousDelegate, delegatee, delegatorBalance);
    }

    function _moveDelegate(
        address from,
        address to,
        secret uint256 amount
    ) internal {
        unchecked {
            bool fromZero = from == address(0);
            if (!fromZero) {
                secret uint256 fromSnapshotCount = _snapshotCount[from];

                // Underflow is impossible since, if from != address(0), then a delegation must have occurred (at least 1 snapshot)
                secret uint256 previous = _snapshots[from][fromSnapshotCount - 1].value;
                uint128 newValue = uint128(previous - amount);

                _writeSnapshot(from, newValue, fromSnapshotCount);
                emit Events.FollowNFTDelegatedPowerChanged(from, newValue, block.timestamp);
            }

            if (to != address(0)) {
                // if from == address(0) then this is an initial delegation (add amount to supply)
                if (fromZero) {
                    // It is expected behavior that the `previousDelSupply` underflows upon the first delegation,
                    // returning the expected value of zero
                    secret uint256 delSupplySnapshotCount = _delSupplySnapshotCount;
                    uint128 previousDelSupply = _delSupplySnapshots[delSupplySnapshotCount - 1]
                        .value;
                    uint128 newDelSupply = uint128(previousDelSupply + amount);
                    _writeSupplySnapshot(newDelSupply, delSupplySnapshotCount);
                }

                // It is expected behavior that `previous` underflows upon the first delegation to an address,
                // returning the expected value of zero
                secret uint256 toSnapshotCount = _snapshotCount[to];
                uint128 previous = _snapshots[to][toSnapshotCount - 1].value;
                uint128 newValue = uint128(previous + amount);
                _writeSnapshot(to, newValue, toSnapshotCount);
                emit Events.FollowNFTDelegatedPowerChanged(to, newValue, block.timestamp);
            } else {
                // If from != address(0) then this is removing a delegation, otherwise we're dealing with a
                // non-delegated burn of tokens and don't need to take any action
                if (!fromZero) {
                    // Upon removing delegation (from != address(0) && to == address(0)), supply calculations cannot
                    // underflow because if from != address(0), then a delegation must have previously occurred, so
                    // the snapshot count must be >= 1 and the previous delegated supply must be >= amount
                    secret uint256 delSupplySnapshotCount = _delSupplySnapshotCount;
                    uint128 previousDelSupply = _delSupplySnapshots[delSupplySnapshotCount - 1]
                        .value;
                    uint128 newDelSupply = uint128(previousDelSupply - amount);
                    _writeSupplySnapshot(newDelSupply, delSupplySnapshotCount);
                }
            }
        }
    }

    function _writeSnapshot(
        address owner,
        uint128 newValue,
        secret uint256 ownerSnapshotCount
    ) internal {
        unchecked {
            uint128 currentBlock = uint128(block.number);
            mapping(secret uint256 => Snapshot) storage ownerSnapshots = _snapshots[owner];

            // Doing multiple operations in the same block
            if (
                ownerSnapshotCount != 0 &&
                ownerSnapshots[ownerSnapshotCount - 1].blockNumber == currentBlock
            ) {
                ownerSnapshots[ownerSnapshotCount - 1].value = newValue;
            } else {
                ownerSnapshots[ownerSnapshotCount] = Snapshot(currentBlock, newValue);
                _snapshotCount[owner] = ownerSnapshotCount + 1;
            }
        }
    }

    function _writeSupplySnapshot(uint128 newValue, secret uint256 supplySnapshotCount) internal {
        unchecked {
            uint128 currentBlock = uint128(block.number);

            // Doing multiple operations in the same block
            if (
                supplySnapshotCount != 0 &&
                _delSupplySnapshots[supplySnapshotCount - 1].blockNumber == currentBlock
            ) {
                _delSupplySnapshots[supplySnapshotCount - 1].value = newValue;
            } else {
                _delSupplySnapshots[supplySnapshotCount] = Snapshot(currentBlock, newValue);
                _delSupplySnapshotCount = supplySnapshotCount + 1;
            }
        }
    }
}
